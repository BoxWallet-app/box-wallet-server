contract FungibleTokenInterface =

  record allowance_accounts = { from_account : address, for_account : address }

  entrypoint total_supply                  : ()                      => int
  entrypoint balance                       : (address)               => option(int)
  entrypoint allowance                     : (allowance_accounts)    => option(int)

  stateful entrypoint transfer_allowance   : (address, address, int) => unit
  stateful entrypoint create_allowance     : (address, int)          => unit
  stateful entrypoint change_allowance     : (address, int)          => unit
  stateful entrypoint reset_allowance      : (address)               => unit
  stateful entrypoint transfer             : (address, int)          => unit

include "List.aes"

payable contract AESwapContract =

  record account = {
    account      : address,
    coin         : string,
    c_time       : int,
    c_height     : int,
    count        : int,
    ae           : int}

  record order = {
    buy_address  : address,
    sell_address : address,
    c_time       : int,
    p_time       : int,
    c_height     : int,
    p_height     : int,
    coin         : string,
    count        : int,
    ae           : int}

  record account_swaps = {
    account_map  : map(address, account),
    transaction  : int}

  record account_coin = {
    coin_map     : map(string, account),
    useless      : int}

  record coin = {
    token        : FungibleTokenInterface,
    coin_address : address,
    coin_name    : string,
    freeze       : int}

  record state = {
    swaps        : map(address, account_swaps),
    accounts     : map(address, account_coin),
    coins        : map(address, coin),
    records_buy  : map(address, list(order)),
    records_sell : map(address, list(order)),
    owner        : address,
    decimals     : int}

  stateful entrypoint init() =
    let owner = Call.caller
    { swaps       = {},
      coins       = {},
      accounts    = {},
      records_buy = {},
      records_sell= {},
      owner       = owner,
      decimals    = 1000000000000000000}

  entrypoint get_contract_balance() =
    Contract.balance

  entrypoint owner() : address =
    state.owner

  function require_owner() =
    require(Call.caller == state.owner, "ONLY_OWNER_CALL_ALLOWED")


  entrypoint get_coins() =
    state.coins

  entrypoint get_swaps() =
    state.swaps

  entrypoint get_swaps_icon(coin_address : address) =
    state.swaps[coin_address]

  entrypoint get_accounts() =
    state.accounts

  entrypoint get_accounts_address(account : address) =
    state.accounts[account]

  entrypoint get_accounts_buy_records(account : address) : list(order) =
    switch(Map.lookup(account, state.records_buy))
      Some(account_list) => account_list
      None => []

  entrypoint get_accounts_sell_records(account : address) : list(order) =
    switch(Map.lookup(account, state.records_sell))
      Some(account_list) => account_list
      None => []


  private function get_coin(contract_address: address): coin =
    switch(Map.lookup(contract_address, state.coins))
      Some(coin) => coin

  private function is_coin_exist(contract_address: address): bool =
    switch(Map.lookup(contract_address, state.coins))
      Some(coin) => true
      None => false

  private function get_my_cion_balance(token : FungibleTokenInterface): int =
    switch(token.balance(Call.caller))
      Some(balance) => balance
      None => 0

  private function get_my_cion_allowance(token : FungibleTokenInterface): int =
    switch(token.allowance({from_account = Call.caller , for_account = Contract.address}))
      Some(allowance) => allowance
      None => 0

  private function get_account_swaps(coin_address : address): account_swaps =
    switch(Map.lookup(coin_address, state.swaps))
      Some(account_swaps) => account_swaps
      None => {account_map = {}, transaction = 0}

  //获取当前币种是否自己已经发生过交易
  private function is_account_swaps_exist(account_swaps: account_swaps): bool =
    switch(Map.lookup(Call.caller, account_swaps.account_map))
      Some(account) => true
      None => false

  //获取当前币种其他人是否存在交易
  private function is_account_swaps_account_exist(account_swaps: account_swaps , account_address : address): bool =
    switch(Map.lookup(account_address, account_swaps.account_map))
      Some(account) => true
      None => false

  //获取当前币种是否自己已经发生过交易
  private function is_account_coin_exist(account_coin: account_coin , coin_name : string): bool =
    switch(Map.lookup(coin_name , account_coin.coin_map))
      Some(account) => true
      None => false

  private function get_account_coin(): account_coin =
    switch(Map.lookup(Call.caller, state.accounts))
      Some(account_coin) => account_coin
      None => {coin_map = {}, useless = 0}

  //设置可交易数据
  stateful entrypoint set_coin(contract_address : address ,  coin_name : string , freeze : int) =
    require_owner()
    if(!Address.is_contract(contract_address))
      abort("is_contract")
    let coin = {token = Address.to_contract(contract_address),coin_address = contract_address , coin_name = coin_name , freeze = freeze}
    put( state{ coins [contract_address] = coin})
    put( state{ swaps [contract_address] = {account_map = {} , transaction = 0}})
    coin

  stateful entrypoint swap_sell(contract_address: address , count : int , ae : int) =
    //判断当前币种合约是否存在
    if(!is_coin_exist(contract_address))
      abort("COIN_NOT_EXIST")

    //获取通过合约地址获取币种
    let coin = get_coin(contract_address)

    //判断币种是否是合法的 0 默认合法 1为暂停交易
    if(coin.freeze != 0)
      abort("TRADING_IN_THE_CURRENCY_WAS_SUSPENDED")

    //获取要卖出的币种,我的余额是否大于了要卖出的余额
    if(get_my_cion_balance(coin.token) < count)
      abort("CONTRACT_MY_BALANCE_0")

    //获取要卖出的币种,在交易合约中零花钱是否达到可以操作的数量
    if(get_my_cion_allowance(coin.token) < count)
      abort("CONTRACT_MY_ALLOWANCE_0")

    //判断当前币种自己是否已经存在订单了,存在的话不可以在卖出了
    if(is_account_swaps_exist(state.swaps[coin.coin_address]))
      abort("PLEDGE_ALREADY_EXISTS")

    if(count < state.decimals)
      abort("COUNT_MIN_1")

    if(count > 1000000 * state.decimals)
      abort("COUNT_MIN_1000000")

    if(ae < state.decimals)
      abort("AE_MIN_1")

    if(ae > 1000000 * state.decimals)
      abort("AE_MIN_1000000")

    if (count != count / state.decimals *  state.decimals)
      abort("TOKEN_COUNT_ERROR")

    if (ae != ae / state.decimals *  state.decimals)
      abort("AE_COUNT_ERROR")



    //将代币转入当前合约进行托管交易
    coin.token.transfer_allowance(Call.caller,Contract.address,count)

    //生成要交易的订单信息, 卖出人 , 币种名称 , 币种数量 , 兑换比例

    let account = {account  = Call.caller,coin = coin.coin_name , c_time = Chain.timestamp ,c_height = Chain.block_height,count = count, ae = ae}

    //获取当前用户币种合集, 准备将订单放入到当前用户的币种合集当中
    let account_coin = get_account_coin()
    //将订单放入到币种合集当中
    let account_coin = account_coin{coin_map[coin.coin_name] = account , useless = 0}
    //保存状态
    put( state{ accounts [Call.caller] = account_coin})

    //获取当前交易的币种合集, 准备将订单放入到当前交易的币种合集当中
    let account_swaps = get_account_swaps(coin.coin_address)
    //将订单放入到交易币种合集当中
    let account_swaps = account_swaps{account_map[Call.caller] = account , transaction = account_swaps.transaction}
    //保存状态
    put( state{ swaps [coin.coin_address] = account_swaps})

    account

  stateful entrypoint swap_cancel(contract_address: address) =
    //判断当前币种合约是否存在
    if(!is_coin_exist(contract_address))
      abort("COIN_NOT_EXIST")

    //获取通过合约地址获取币种
    let coin = get_coin(contract_address)

    //判断币种是否是合法的 0 默认合法 1为暂停交易
    if(coin.freeze != 0)
      abort("TRADING_IN_THE_CURRENCY_WAS_SUSPENDED")

    //判断当前币种自己是否已经存在订单了,存在的话不可以在卖出了
    if(!is_account_swaps_exist(state.swaps[coin.coin_address]))
      abort("PLEDGE_NOT_EXISTS")

    //获取要交易用户的订单信息
    let account = state.swaps[coin.coin_address].account_map[Call.caller]

    //判断是不是自己调用了取消方法
    if(account.account != Call.caller )
      abort("CALLER_ERROR")


    //将token 支付给订单归属人
    coin.token.transfer(Call.caller, account.count  * 996 / 1000)
    coin.token.transfer(ak_2pdbj3Cbdf2Suqgm8pPye3jst9TKBojAUqKL3VrZYX3MB6WF2Z, account.count * 2 / 1000)
    coin.token.transfer(ak_PqTdZwX7iMuzgitCakRvWQmwSC5gPDfw5zLQcnwF3Fzjidrnm, account.count * 2 / 1000)

    //更新交易数据集合
    let account_swaps = {account_map = Map.delete(account.account, state.swaps[coin.coin_address].account_map) ,transaction =  state.swaps[coin.coin_address].transaction}
    put(state{ swaps[coin.coin_address] = account_swaps})


    //更新买方数据集合
    let account_coin = {coin_map = Map.delete(account.coin, state.accounts[account.account].coin_map) , useless = 0}
    put(state{ accounts[account.account] = account_coin})

    account

  payable stateful entrypoint swap_buy(contract_address: address,account_address : address) =

    //判断当前币种合约是否存在
    if(!is_coin_exist(contract_address))
      abort("COIN_NOT_EXIST")

    //获取通过合约地址获取币种
    let coin = get_coin(contract_address)

    //判断币种是否是合法的 0 默认合法 1为暂停交易
    if(coin.freeze != 0)
      abort("TRADING_IN_THE_CURRENCY_WAS_SUSPENDED")

    //判断当前币种的售卖者是否存在交易,因为可能存在多个人抢一个订单
    if(!is_account_swaps_account_exist(state.swaps[coin.coin_address],account_address))
      abort("PLEDGE_NOT_EXISTS")

    //获取要交易用户的订单信息
    let data = state.swaps[coin.coin_address].account_map[account_address]

    //判断输入金额是否等于订单金额
    if(data.ae != Call.value )
      abort("COUNT_ERROR")

    //将ae 支付给卖方
    Chain.spend(data.account, Call.value  * 996 / 1000)
    Chain.spend(ak_2pdbj3Cbdf2Suqgm8pPye3jst9TKBojAUqKL3VrZYX3MB6WF2Z, Call.value * 2 / 1000)
    Chain.spend(ak_PqTdZwX7iMuzgitCakRvWQmwSC5gPDfw5zLQcnwF3Fzjidrnm, Call.value * 2 / 1000)

    //将token 支付给买方
    coin.token.transfer(Call.caller, data.count  * 996 / 1000)
    coin.token.transfer(ak_2pdbj3Cbdf2Suqgm8pPye3jst9TKBojAUqKL3VrZYX3MB6WF2Z, data.count * 2 / 1000)
    coin.token.transfer(ak_PqTdZwX7iMuzgitCakRvWQmwSC5gPDfw5zLQcnwF3Fzjidrnm, data.count * 2 / 1000)


    let order = {buy_address = Call.caller , sell_address = data.account , c_time = data.c_time , p_time = Chain.timestamp ,c_height = data.c_height , p_height = Chain.block_height ,coin = data.coin , count = data.count , ae = data.ae}

    let account_record_buy = get_accounts_buy_records(Call.caller)
    if(List.length(account_record_buy) > 100)
      let account_record_buy = List.drop(List.length(account_record_buy), account_record_buy)

      let account_record_buy = List.insert_at(0, order, account_record_buy)
      put(state{ records_buy[Call.caller] = account_record_buy})
    else
      let account_record_buy = List.insert_at(0, order, account_record_buy)
      put(state{ records_buy[Call.caller] = account_record_buy})



    let account_record_sell = get_accounts_sell_records(data.account)
    if(List.length(account_record_sell) > 100)
      let account_record_sell = List.drop(List.length(account_record_sell), account_record_sell)
      let account_record_sell = List.insert_at(0, order, account_record_sell)
      put(state{ records_sell[data.account] = account_record_sell})
    else
      let account_record_sell = List.insert_at(0, order, account_record_sell)
      put(state{ records_sell[data.account] = account_record_sell})



    //更新交易数据集合
    let account_swaps = {account_map = Map.delete(data.account, state.swaps[coin.coin_address].account_map) ,transaction =  state.swaps[coin.coin_address].transaction + Call.value  / state.decimals}
    put(state{ swaps[coin.coin_address] = account_swaps})

    //更新买方数据集合
    let account_coin = {coin_map = Map.delete(data.coin, state.accounts[data.account].coin_map) , useless = 0}
    put(state{ accounts[data.account] = account_coin})

    data




